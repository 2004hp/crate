/*
 * Licensed to Crate.io Inc. (Crate) under one or more contributor license
 * agreements.  See the NOTICE file distributed with this work for additional
 * information regarding copyright ownership.  Crate licenses this file to
 * you under the Apache License, Version 2.0 (the "License");  you may not
 * use this file except in compliance with the License.  You may obtain a
 * copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS, WITHOUT
 * WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.  See the
 * License for the specific language governing permissions and limitations
 * under the License.
 *
 * However, to use any modules in this file marked as "Enterprise Features",
 * Crate must have given you permission to enable and use such Enterprise
 * Features and you must have a valid Enterprise or Subscription Agreement
 * with Crate.  If you enable or use the Enterprise Features, you represent
 * and warrant that you have a valid Enterprise or Subscription Agreement
 * with Crate.  Your use of the Enterprise Features if governed by the terms
 * and conditions of your Enterprise or Subscription Agreement with Crate.
 */

apply from: "$rootDir/gradle/javaModule.gradle"


archivesBaseName = 'crate-core'

dependencies {
    compile project(':es:es-core')
    testCompile project(':integration-testing')
}

test {
    outputs.upToDateWhen { false }

    testLogging.exceptionFormat = 'full'
    jacoco {
        excludes = [
                "*Test*"
        ]
    }
}

task getVersion(dependsOn: 'classes') {
    doLast {
        def stdout = new ByteArrayOutputStream()
        javaexec {
            classpath = sourceSets.main.runtimeClasspath
            main = 'io.crate.Version'
            standardOutput = stdout
        }
        ext.gitTag = "git describe".execute().in.text.trim()
        ext.version = stdout.toString().trim().split(" ")[1].replace(',', '').trim()

        if (gradle.taskGraph.hasTask(':app:release')) {
            assert gitTag == version, "Version mismatch gitTag: " + gitTag + " does not match crate version: " + version
        } else if (gradle.taskGraph.hasTask(':app:nightly')) {
            def date = new Date().format('yyyyMMddHHmm')
            def shortHash = "git rev-parse --short HEAD".execute().in.text.trim()
            ext.version = version.replace('-SNAPSHOT', '') + "-" + date + "-" + shortHash
        } else if (!gradle.taskGraph.hasTask(':app:uploadArchives')) {
            def shortHash = "git rev-parse --short HEAD".execute().in.text.trim()
            ext.version = version + "-" + shortHash
        }

        logger.quiet("gitTag: " + gitTag)
        logger.quiet("version: " + version)
    }
}


sourceSets {
    test.output.resourcesDir = null
}
